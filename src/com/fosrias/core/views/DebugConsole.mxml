<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas 
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:mx="library://ns.adobe.com/flex/halo" 
    width="100%" height="100%" tabEnabled="false"
    creationComplete="onComplete();">

<!-- Define Script _________________________________________________________ -->     
<fx:Script>
<![CDATA[
	import com.fosrias.core.events.DebugEvent;
	import com.fosrias.core.models.DebugMessage;
	
	import mx.core.FlexGlobals;
    
    //--------------------------------------------------------------------------
    //
    //  Variables
    //
    //--------------------------------------------------------------------------
    
    private var _count:Number = 0;
    private var _messages:String;  
    private var _lastTime:Number;
    
    //--------------------------------------------------------------------------
    //
    //  Methods
    //
    //--------------------------------------------------------------------------
    
    //Adds listener to update debug messages sent to the main application.
    private function onComplete():void
    {
		FlexGlobals.topLevelApplication.addEventListener(DebugEvent.SET_MESSAGE, 
    	setMessage);
    }
    
    //Handler for DebugEvent.SET_MESSAGE event.
    private function setMessage(event:DebugEvent):void
    {
    	var message:DebugMessage = event.message;
    	var messageTime:Number = message.time.valueOf();
        if ( chkEvents.selected != message.isEventMessage 
            && message.isEventMessage )
            return;
        
        if (_messages == null) 
        {
        	_count = 1;
        	message.count = _count;
            _messages = message.timeStampedMessage;
            _lastTime = messageTime;
        } else {
        	if (messageTime != _lastTime)
        	{
        		message.count = 1;
        		_messages = message.timeLapse(_lastTime) + 
        		     "\n===============\n" + _messages;
        		_lastTime = messageTime;
        		_count = 1;
        	}
        	_count += 1;
            message.count = _count;
        	_messages =  message.timeStampedMessage + "\n" +  _messages;
        }
        updateMessages();
    }

    //Clears messages in the console.
    private function clearMessages():void 
    {
        _messages = null;
        updateMessages();
    }
    
    //Updates messages in the console.
    private function updateMessages():void 
    {
        taDebug.text = _messages;
    }
    
]]>
</fx:Script>

    <!-- Define GUI ________________________________________________________ -->   
    <mx:TextArea id="taDebug" editable="false" 
    	tabEnabled="false" focusThickness="0" left="10" top="10" bottom="39" right="10"/>
    <mx:Button label="clear" click="clearMessages();" bottom="10" left="10"/>
    <s:CheckBox id="chkEvents" label="Show Events" bottom="12" left="95"/>
</mx:Canvas>